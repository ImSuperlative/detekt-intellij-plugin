import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.process.CommandLineArgumentProvider
import org.jetbrains.intellij.platform.gradle.TestFrameworkType

plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.intellijPlatform)
    alias(libs.plugins.versions)
    alias(libs.plugins.github.release)
}

project.group = "io.gitlab.arturbosch.detekt"
project.version = libs.versions.detektIJ.get()

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    constraints {
        runtimeOnly(libs.slf4j.api) {
            because("transitive ktlint logging dependency (2.0.3) does not use the module classloader in ServiceLoader")
        }
    }

    implementation(libs.detekt.api)
    implementation(libs.detekt.tooling)

    runtimeOnly(libs.detekt.core)
    runtimeOnly(libs.detekt.rules)
    runtimeOnly(libs.detekt.ktlintWrapper)

    testImplementation(libs.detekt.testUtils)
    testImplementation(libs.assertj.core)
    testImplementation(libs.junit.jupiter)

    testRuntimeOnly(libs.junit.platform)
    testRuntimeOnly(libs.junit4)

    intellijPlatform {
        intellijIdea("2026.1")

        bundledPlugin("com.intellij.java")
        bundledPlugin("org.intellij.intelliLang")
        bundledPlugin("org.jetbrains.kotlin")

        testFramework(TestFrameworkType.Platform)

        pluginVerifier()
    }
}

listOf(
    configurations.compileClasspath,
    configurations.runtimeClasspath,
    configurations.testCompileClasspath,
    configurations.testRuntimeClasspath,
).forEach {
    it.configure {
        exclude(group = "org.jetbrains.kotlinx", module = "kotlinx-coroutines-bom")
        exclude(group = "org.jetbrains.kotlinx", module = "kotlinx-coroutines-core")
        exclude(group = "org.jetbrains.kotlinx", module = "kotlinx-coroutines-core-jvm")
        exclude(group = "org.jetbrains.kotlinx", module = "kotlinx-coroutines-test")
        exclude(group = "org.jetbrains.kotlinx", module = "kotlinx-coroutines-test-jvm")
    }
}

kotlin {
    jvmToolchain(21)
}

tasks.withType<Test>().configureEach {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        showExceptions = true
        showCauses = true
        exceptionFormat = TestExceptionFormat.FULL
    }
}

tasks.publishPlugin {
    // This property can be configured via environment variable ORG_GRADLE_PROJECT_intellijPublishToken
    // See: https://docs.gradle.org/current/userguide/build_environment.html#sec:project_properties
    token.set((findProperty("intellijPublishToken") as? String).orEmpty())
    // https://plugins.jetbrains.com/docs/intellij/publishing-plugin.html#specifying-a-release-channel
    // "-beta" is used for pre-releases and https://plugins.jetbrains.com/plugins/beta/list as plugin repository.
    channels.set(listOf(project.version.toString().split('-').getOrElse(1) { "default" }.split('.').first()))
}

intellijPlatform {
    pluginConfiguration {
        name.set("Detekt IntelliJ Plugin")

        ideaVersion {
            sinceBuild = "261"
            untilBuild = provider { null }
        }
    }

    pluginVerification {
        ides { recommended() }
    }
}

githubRelease {
    token((project.findProperty("github.token") as? String).orEmpty())
    owner.set("detekt")
    repo.set("detekt-intellij-plugin")
    targetCommitish.set("main")
    overwrite.set(true)
    dryRun.set(false)
    body.set(
        provider {
            var changelog = project.file("changelog.md").readText()
            val sectionStart = "#### ${project.version}"
            changelog = changelog.substring(changelog.indexOf(sectionStart) + sectionStart.length)
            changelog = changelog.substring(0, changelog.indexOf("#### 1"))
            changelog.trim()
        }
    )
    val distribution = project.layout.buildDirectory
        .file("distributions/Detekt IntelliJ Plugin-${project.version}.zip")
    releaseAssets.setFrom(distribution)
}

tasks.githubRelease.configure {
    dependsOn(tasks.buildPlugin)
}

tasks.prepareSandbox {
    sandboxDirectory = project.layout.projectDirectory.dir(".sandbox")
}

tasks.runIde {
    jvmArgumentProviders +=
        CommandLineArgumentProvider {
            listOf(
                "--add-opens",
                "java.base/java.lang=ALL-UNNAMED",
                "-XX:+UnlockDiagnosticVMOptions",
            )
        }
}
